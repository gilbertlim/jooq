
String dbUser   = System.properties["db-user"]   ?: "root"
String dbPasswd = System.properties["db-passwd"] ?: "passwd"

jooq {
    version = "${jooqVersion}"
    configurations {
        sakilaDB { // Gradle Task 명
            generationTool {
                jdbc { // DB 접근 관련 설정
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:13306'
                    user = "${dbUser}"
                    password = "${dbPasswd}"
                }
                generator { // DSL을 생성하기위한 설정
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database { // DB를 기준으로 DSL을 생성
                        name = 'org.jooq.meta.mysql.MySQLDatabase'

                        //unsignedTypes = false // Unsigned Type을 사용하지 않도록 설정(Integer)
                        unsignedTypes = true // true로 두고, forcedTypes를 통해 타입을 강제변환
                        forcedTypes {
                            forcedType {
                                userType = 'java.lang.Long'
                                includeTypes = 'int unsigned'
                            }
                            forcedType {
                                userType = 'java.lang.Integer'
                                includeTypes = 'tinyint unsigned'
                            }
                            forcedType {
                                userType = 'java.lang.Integer'
                                includeTypes = 'smallint unsigned'
                            }
                        }

                        schemata {
                            schema {
                                inputSchema = 'sakila'
                            }
                        }
                    }
                    generate { // DSL 생성 옵션
                        daos = true
                        records = true
                        fluentSetters = true
                        javaTimeTypes = true
                        deprecated = false
                    }
                    target { // DSL이 생성되는 폴더
                        directory = 'src/generated'
                    }

                    // 왜 서브모듈로 분리해야할까 ?
                    strategy.name = 'jooq.custom.generator.JPrefixGeneratorStrategy'
                }
            }
        }
    }
}

sourceSets { // src/generated 폴더의 파일들도 소스에 포함
    main {
        java {
            srcDirs = ["src/main/java", "src/generated"]
        }
    }
}